locals {
  # Lambdas can only be deployed from s3 buckets in the same region. These are
  # the regions where we currently host our lambda code
  supported_regions = ["us-east-1", "us-east-2", "us-west-2"]
  lambda_s3_bucket  = "braintrust-assets-${data.aws_region.current.name}"
  # This file is generated by the release process and contains the path to the
  # latest lambda versions for each lambda function
  lambda_versions          = jsondecode(file("${path.module}/VERSIONS.json"))
  postgres_url             = "postgres://${var.postgres_username}:${var.postgres_password}@${var.postgres_host}:${var.postgres_port}/postgres"
  brainstore_url           = var.brainstore_enabled ? "http://${var.brainstore_hostname}:${var.brainstore_port}" : ""
  brainstore_s3_bucket     = var.brainstore_enabled ? var.brainstore_s3_bucket_name : ""
  clickhouse_secret_string = var.clickhouse_secret_id != null ? data.aws_secretsmanager_secret_version.clickhouse_secret[0].secret_string : ""
  clickhouse_pg_url        = var.clickhouse_host != null ? "postgres://default:${local.clickhouse_secret_string}@${var.clickhouse_host}:9005/default" : ""
  clickhouse_connect_url   = var.clickhouse_host != null ? "http://default:${local.clickhouse_secret_string}@${var.clickhouse_host}:8123/default" : ""
}

data "aws_secretsmanager_secret_version" "clickhouse_secret" {
  count = var.clickhouse_secret_id != null ? 1 : 0
  # The secret ID is a pipe delimited combination of secret ID and version ID
  # https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/secretsmanager_secret_version#attribute-reference
  secret_id  = split("|", var.clickhouse_secret_id)[0]
  version_id = split("|", var.clickhouse_secret_id)[1]
}

data "aws_region" "current" {
  lifecycle {
    postcondition {
      condition     = contains(local.supported_regions, self.name)
      error_message = "Region must be one of: us-east-1, us-east-2, us-west-2. Contact support if you need a new region to be supported."
    }
  }
}

data "aws_caller_identity" "current" {}
